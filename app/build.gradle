plugins {
    alias(libs.plugins.android.application)
}

def parseAbis = { Object value, List<String> fallback ->
    def v = (value ?: "").toString().trim()
    v ? v.split(',').collect { it.trim() }.findAll { it } : fallback
}

def unionsDebugAbis = parseAbis(findProperty("UNIONS_DEBUG_ABIS"), ['arm64-v8a', 'x86_64'])
//def unionsReleaseAbis = parseAbis(findProperty("UNIONS_RELEASE_ABIS"), ['armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'])
def unionsReleaseAbis = parseAbis(findProperty("UNIONS_RELEASE_ABIS"), ['arm64-v8a'])

android {
    compileSdk 35
    namespace 'com.igorantivirus.unions'

    buildFeatures { prefab true }

    defaultConfig {
        applicationId "com.igorantivirus.unions"
        minSdk 21
        targetSdk 35
        versionCode 1
        versionName "1.2.1"

        ndk {
            abiFilters(*unionsReleaseAbis)
        }

        externalNativeBuild {
            cmake {
                arguments '-DEXTERNAL_DIR="D:/dev/libraries/_install"'
                cppFlags '-std=c++23'
                abiFilters(*unionsReleaseAbis)
            }
        }
    }

    buildTypes {
        debug {
            ndk {
                abiFilters(*unionsDebugAbis)
            }
            externalNativeBuild {
                cmake {
                    abiFilters(*unionsDebugAbis)
                }
            }
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    externalNativeBuild {
        cmake {
            version '3.22.1'
            path file('../CMakeLists.txt')
        }
    }
    sourceSets {
        main {
            assets {
                srcDirs '../assets'
            }
        }
    }

    packaging {
        resources {
            excludes += [
                    'META-INF/AL2.0',
                    'META-INF/LGPL2.1',
                    'META-INF/LICENSE',
                    'META-INF/LICENSE.txt',
                    'META-INF/LICENSE.md',
                    'META-INF/LICENSE*',
                    'META-INF/NOTICE',
                    'META-INF/NOTICE.txt',
                    'META-INF/NOTICE.md',
                    'META-INF/NOTICE*',
            ]
        }
    }
}

dependencies {

    implementation files('libs/SDL3-3.4.0.aar')
    implementation files('libs/SDL3_image-3.4.0.aar')
    implementation files('libs/SDL3_mixer-3.1.2.aar')
}
